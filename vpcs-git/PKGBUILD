# vim:set ts=2 sw=2 et:
pkgname=vpcs-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Virtual PC Simulator"
arch=('i686' 'x86_64')
url="https://github.com/GNS3/vpcs.git"
license=('UNK')
makedepends=('git')
source=("vpcs::git://github.com/GNS3/vpcs.git")
sha256sums=('SKIP')
conflicts=('vpcs')
provides=('vpcs')

pkgver() {
  cd "$srcdir/vpcs"

  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
  [ $PIPESTATUS -ne 0 ] && git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
  [ $PIPESTATUS -ne 0 ] && printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"

}

build() {
  cd "$srcdir/vpcs/src"

  # fix the makefile hardcoding i386
  if test "$CARCH" == x86_64; then
    sed -i "s/i386/x86_64/" Makefile.linux
  fi
  # fix the makefile forcing a static binary
  sed -i "s/-s -static//" Makefile.linux
  make -f ./Makefile.linux
}
package() {
  install -D -m 0755 "${srcdir}/vpcs/src/vpcs" "${pkgdir}/usr/bin/vpcs"
  install -D -m 0644 "${srcdir}/vpcs/readme.txt" "${pkgdir}/usr/share/vpcs/readme.txt"

  install -D -m 0644 "${srcdir}/vpcs/src/startup" "${pkgdir}/usr/share/vpcs/startup.vpc"
  install -D -m 0644 "${srcdir}/vpcs/COPYING" "${pkgdir}/usr/share/licenses/vpcs/COPYING"
}

